plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1' apply false
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    minecraft = minecraft_version
}

static def listGameVersions(String string1, String string2) {
    // Extract the base version and the starting and ending patch numbers
    String baseVersion = string1
    int startPatch = 0
    if(string1.split('\\.').length > 2) {
        startPatch = Integer.parseInt(string1.split('\\.')[2])
    }
    int endPatch = Integer.parseInt(string2.split('\\.')[2])

    // Initialize an array to hold the generated versions
    def versions = []

    // Add the base version (string1) to the array
    versions << baseVersion

    // Generate and add the patch versions
    for (int i = startPatch + 1; i <= endPatch; i++) {
        versions << "${baseVersion}.${i}"
    }

    // Return the generated versions
    return versions
}


subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.minecraft:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            it.officialMojangMappings {
                setNameSyntheticMembers(false)
            }
            it.parchment("org.parchmentmc.data:parchment-${rootProject.parchment_version}")
        }
    }

    var env = System.getenv()
    var modLoader = name

    var changelogFile = rootProject.file("changelog.md")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""

    var versions = listGameVersions(minecraft_min_version, minecraft_version)

    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = modrinth_id
            uploadFile = tasks.remapJar
            gameVersions = versions
            if (modLoader == "forge") {
                loaders = ["neoforge"]
            } else loaders = [modLoader]

            changelog = changelogText
            versionNumber =  mod_version +"-"+ modLoader
            versionType = release_type

            if (modLoader == "fabric") {
                dependencies {
                    required.project "fabric-api"
                    required.project "moonlight"
                    required.project "every-compat"
                }
            } else {
                versionName = mod_id +"-"+ mod_version
                dependencies {
                    required.project "moonlight"
                    required.project "every-compat"
                }
            }
        }
    }

    if (env.CURSEFORGE_TOKEN) {
        curseforge {
            apiKey = env.CURSEFORGE_TOKEN
            project {
                id = project_id
                changelog = changelogText
                changelogType = 'markdown'
                releaseType = release_type
                for (var ver : versions){
                    addGameVersion ver
                }
                if (modLoader == "neoforge") {
                    addGameVersion("NeoForge")
                } else addGameVersion modLoader.capitalize()

                mainArtifact(tasks.remapJar)

                if (modLoader == "fabric") {
                    relations {
                        requiredDependency("fabric-api")
                        requiredDependency("selene")
                        requiredDependency("every-compat")
                        optionalDependency("additional-lights")
                        optionalDependency("building-but-better")
                        optionalDependency("better-archeology")
                        optionalDependency("create-fabric")
//                        optionalDependency("decorative-blocks")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-windows")
                        optionalDependency("macaws-stairs")
//                        optionalDependency("mighty-mail-fabric")
                        optionalDependency("more-beautiful-torches")
                        optionalDependency("stoneworks")
                        optionalDependency("twigs")
                        optionalDependency("rechiseled")
                    }
                } else {
                    relations {
                        requiredDependency("selene")
                        requiredDependency("every-compat")
                        optionalDependency("additional-lights")
                        optionalDependency("better-archeology")
                        optionalDependency("blockus")
//                        optionalDependency("buildersaddition")
                        optionalDependency("building-but-better")
//                        optionalDependency("corail-pillar")
                        optionalDependency("create")
//                        optionalDependency("decorative-blocks")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-windows")
                        optionalDependency("macaws-stairs")
//                        optionalDependency("mighty-mail")
                        optionalDependency("more-beautiful-torches")
                        optionalDependency("quark")
                        optionalDependency("stone-chest")
                        optionalDependency("stoneworks")
                        optionalDependency("twigs")
                        optionalDependency("rechiseled")
                    }
                }
            }
        }
    }

}

allprojects {

    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = mod_id
    version = mod_version
    group = mod_group_id

    repositories {

        flatDir {
            dirs 'mods'
        }

        mavenCentral()
        mavenLocal()
        maven { url = 'https://maven.neoforged.net' }
        maven { url = "https://maven.parchmentmc.org" }
        maven { url = "https://www.cursemaven.com"; allowInsecureProtocol = true }
        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://mvn.devos.one/snapshots/" }
        maven { url = "https://mvn.devos.one/releases" } // Porting Lib Releases
        maven { url = "https://maven.shedaniel.me/" } // Cloth Config
        maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // Forge Config API Port
        maven { url = "https://maven.jamieswhiteshirt.com/libs-release";
            content { includeGroup("com.jamieswhiteshirt") }
        } // Reach Entity Attributes
        maven { url = "https://squiddev.cc/maven" } // CC: Tweaked
        maven { name = 'Ladysnake Mods'; url = 'https://maven.ladysnake.org/releases' }
        maven { name = "Jared's maven"; url = "https://maven.blamejared.com/" } // JEI
        maven { name = "ModMaven"; url = "https://modmaven.dev" } // Botania & FALLBACK for JEI
        maven { url = "https://maven.teamresourceful.com/repository/maven-public/" } // Chipped
        maven { url = "https://maven.firstdarkdev.xyz/snapshots" }
        maven { name = "TerraformersMC"; url = "https://maven.terraformersmc.com/" }
        maven { url = "https://raw.githubusercontent.com/moddingplayground/maven/main/" }
        maven { url = 'https://maven.ambertation.de/releases' } // BCLib, BetterEnd, WunderLib
        maven { url 'https://maven.wispforest.io' }
        maven { url 'https://maven.su5ed.dev/releases' }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = rootProject.java_version.toInteger()
    }

    java {
        withSourcesJar()
    }
}

ext {
    isMyPc =  System.getenv().MY_PC == "1"
    print "isMyPc: $isMyPc"
    replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_min_version  : minecraft_min_version,
            minecraft_max_version  : minecraft_max_version,
            pack_format_number     : pack_format_number,

            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_license            : mod_license,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            mod_credits            : mod_credits,
            mod_homepage           : mod_homepage,
            mod_github             : mod_github,

            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,

            moonlight_version_range: moonlight_version_range,
    ]
}


tasks.register('buildAndPublishAll') {
    dependsOn ':clean'
    dependsOn ':build'
    dependsOn ':neoforge:curseforge'
    dependsOn ':neoforge:modrinth'
    dependsOn ':fabric:curseforge'
    dependsOn ':fabric:modrinth'
}

buildAndPublishAll.group = 'build'
buildAndPublishAll.description = 'Runs clean, build, and publish forge and fabric'

tasks.register('buildAndPublishAllM') {
    dependsOn buildAndPublishAll
    dependsOn ':publishToMavenLocal'
}

buildAndPublishAllM.group = 'build'
buildAndPublishAllM.description = 'Runs clean, build, publish forge and fabric and uploads to maven local'

