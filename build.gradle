plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.github.johnrengelman.shadow"


    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            it.officialMojangMappings {
                setNameSyntheticMembers(false)
            }
            it.parchment("org.parchmentmc.data:parchment-${rootProject.parchment_version}")
        }
    }

    var env = System.getenv()
    var modLoader = name

    var changelogFile = rootProject.file("changelog.md")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""

    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = "stone-zone"
            uploadFile = tasks.remapJar
            gameVersions = [rootProject.minecraft_version]
            loaders = [modLoader]
            changelog = changelogText

            if (modLoader == "fabric") {
                versionName = mod_id +"-"+ mod_version + "-fabric"
                dependencies {
                    required.project "fabric-api"
                    required.project "moonlight"
                    required.project "every-compat"
                }
            } else {
                versionName = mod_id +"-"+ mod_version
                dependencies {
                    required.project "moonlight"
                    required.project "every-compat"
                }
            }
        }
    }

    if (env.CURSEFORGE_TOKEN) {
        curseforge {
            apiKey = env.CURSEFORGE_TOKEN
            project {
                id = rootProject.project_id
                changelog = changelogText
                changelogType = 'markdown'
                releaseType = 'release'
                addGameVersion rootProject.minecraft_version
                addGameVersion modLoader.capitalize()

                mainArtifact(tasks.remapJar) {
                    if (modLoader == "fabric") {
                        displayName = mod_id +"-"+ mod_version + "-fabric"
                    }
                }

                if (modLoader == "fabric") {
                    relations {
                        requiredDependency("fabric-api")
                        requiredDependency("selene")
                        requiredDependency("every-compat")
                        optionalDependency("building-but-better")
                        optionalDependency("create-fabric")
                        optionalDependency("decorative-blocks")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-doors")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-furniture")
                        optionalDependency("macaws-lights-and-lamps")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-trapdoors")
                        optionalDependency("macaws-windows")
                        optionalDependency("mighty-mail-fabric")
                        optionalDependency("twigs")
                    }
                } else {
                    relations {
                        requiredDependency("selene")
                        requiredDependency("every-compat")
                        optionalDependency("buildersaddition")
                        optionalDependency("building-but-better")
                        optionalDependency("corail-pillar")
                        optionalDependency("create")
                        optionalDependency("decorative-blocks")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-doors")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-furniture")
                        optionalDependency("macaws-lights-and-lamps")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-trapdoors")
                        optionalDependency("macaws-windows")
                        optionalDependency("mighty-mail")
                        optionalDependency("twigs")
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.mod_id
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {

        flatDir {
            dirs 'mods'
        }

        mavenLocal()

        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url "https://maven.parchmentmc.org" }

        maven {
            url = "https://www.cursemaven.com"
            allowInsecureProtocol = true
        }

        maven { url = 'https://jitpack.io' } // BCLib

        maven { url = "https://api.modrinth.com/maven" }

        maven { url = "https://mvn.devos.one/snapshots/" } // registrate_fabric & registrate

        maven { url = "https://mvn.devos.one/releases" } // Porting Lib Releases

        maven { url = "https://maven.shedaniel.me/" } // Cloth Config

        // Required for Create
        maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel

        maven { url = "https://maven.tterrag.com" } // Registrate

        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Forge Config API Port

        maven { // Reach Entity Attributes
            url = "https://maven.jamieswhiteshirt.com/libs-release"
            content { includeGroup("com.jamieswhiteshirt") }
        }
        maven { // CC: Tweaked
            url = "https://squiddev.cc/maven"
        }
        maven { // Unknown
            name = 'Ladysnake Mods'
            url = 'https://maven.ladysnake.org/releases'
        }
        maven { // JEI
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven { // Botania & FALLBACK for JEI
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
